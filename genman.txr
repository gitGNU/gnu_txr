@# This requires the man2html which was originally known as vh-man2html
@# and is now part of manutils.
@# See here: http://primates.ximian.com/~flucifredi/man/
@# It does not work with that other man2html written in Perl.
@(deffilter month_name 
  ("01" "Jan") ("02" "Feb") ("03" "Mar") ("04" "Apr")
  ("05" "May") ("06" "Jun") ("07" "Jul") ("08" "Aug")
  ("09" "Sep") ("10" "Oct") ("11" "Nov") ("12" "Dec"))
Content-type: text/html
@(skip 15)
<H1>TXR</H1>
@(skip)Updated: @YEAR-@MONTH-@DAY<BR><A HREF="#index">Index</A>
@(collect)
@PREAMBLE
@(until)
txr - text processing language @(skip)
@(end)
@(all)
txr - text processing language @VERSION
@  (and)
@  (collect)
@BODY
@  (until)
<HR>
<A NAME="index">&nbsp;</A><H2>Index</H2>
@  (end)
@(end)
<HR>
@(collect)
@TOC
@(until)
This document was created by
@(end)
@(output)
<HTML>
<FONT FACE="courier">
<HEAD><TITLE>Manpage for txr @VERSION</TITLE>
</HEAD><BODY>
<H2>Manpage for <A HREF="#lbAB">Txr </a>@VERSION</H2>
<H2>@{MONTH :filter month_name} @DAY, @YEAR</H2>
@(repeat)
@TOC
@(end)
@(repeat)
@PREAMBLE
@(first)
@(end)
@(repeat)
@BODY
@(end)
</BODY>
</HTML>
@(end)
